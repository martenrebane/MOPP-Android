apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    namespace 'ee.ria.DigiDoc'
    compileSdk project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        applicationId 'ee.ria.DigiDoc'
        versionCode Integer.parseInt(appVersionCode)
        versionName appVersionName

        multiDexEnabled true

        ndk {
            abiFilters = []
            abiFilters.addAll(appAbiFilters.split(';').collect{it as String})
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        develop {
            debuggable true
            applicationIdSuffix '.develop'
            signingConfig signingConfigs.debug
            matchingFallbacks = ['debug']
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        lintConfig file('../lint.xml')
    }

    buildFeatures {
        buildConfig = true
    }
}

dependencies {
    implementation "androidx.annotation:annotation:${androidxVersion}"
    implementation "androidx.appcompat:appcompat:${androidxAppCompatVersion}"
    implementation "com.google.android.material:material:${androidMaterialVersion}"
    implementation "androidx.recyclerview:recyclerview:${androidxRecyclerviewVersion}"
    implementation "androidx.cardview:cardview:${androidxCardviewVersion}"
    implementation "androidx.lifecycle:lifecycle-viewmodel:${androidxLifecycleVersion}"
    implementation "androidx.work:work-runtime:${androidxWorkRuntime}"

    implementation "com.takisoft.preferencex:preferencex:${preferencexVersion}"

    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "com.squareup.okio:okio:${okioVersion}"

    implementation "commons-io:commons-io:${commonsioVersion}"

    implementation "com.jakewharton.timber:timber:${timberVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation "com.github.tony19:logback-android:${logbackAndroidVersion}"

    implementation "org.bouncycastle:bcpkix-jdk15on:${bouncycastleVersion}"

    implementation "blue.aodev:material-values:${materialValuesVersion}"

    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${androidxConstraintlayoutVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    implementation "com.bluelinelabs:conductor:${conductorVersion}"

    implementation "io.reactivex.rxjava3:rxjava:${rxJavaVersion}"
    implementation "io.reactivex.rxjava3:rxandroid:${rxAndroidVersion}"
    implementation "com.jakewharton.rxbinding4:rxbinding:${rxBindingVersion}"
    implementation "com.jakewharton.rxbinding4:rxbinding-material:${rxBindingVersion}"

    implementation "org.apache.commons:commons-text:${commonsTextVersion}"
    implementation "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
    annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:${autoValueParcelVersion}"
    implementation "com.ryanharter.auto.value:auto-value-parcel-adapter:${autoValueParcelVersion}"

    implementation "net.cachapa.expandablelayout:expandablelayout:${expandablelayoutVersion}"

    implementation platform("com.google.firebase:firebase-bom:${firebaseCrashlyticsVersion}")
    implementation "com.google.firebase:firebase-crashlytics"

    implementation project(':common-lib')
    implementation project(':sign-lib')
    implementation project(':crypto-lib')
    implementation project(':smart-card-reader-lib')
    implementation project(':id-card-lib')
    implementation project(':mobile-id-lib')
    implementation project(':smart-id-lib')

    testAnnotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
    androidTestImplementation "junit:junit:${junitVersion}"
    androidTestImplementation "androidx.test.ext:junit:${androidxTestVersion}"
    androidTestImplementation "androidx.test:rules:${androidxTestRulesVersion}"
}
